pragma solidity 0.4.15;

import "./BaseMultiSig.sol";

/// @title An RLP encoded transaction version of the SimpleMultiSig contract by Christian Lundkvist.
/// @author Ignacio Baixas (ignacio0buda.com)
contract SimpleMultiSig is BaseMultiSig {
  /// @param threshold_ The multisig signature threshold
  /// @param owners_ The wallet allowed signers adresses, in ascending order.
  function SimpleMultiSig(uint8 threshold_, address[] owners_) {
    setup(threshold_, owners_);
  }

  /// @notice Executes a signed transaction. The signatures must be passed in the same order as
  /// their corresponding signers address ordered in an ascending manner.
  /// @param sigV_ The signatures V parts
  /// @param sigR_ The signatures R parts
  /// @param sigS_ The signatures S parts
  /// @param tx_ A RLP encoded standard ethereum transaction (as generated by most client)
  function execute(uint8[] sigV_, bytes32[] sigR_, bytes32[] sigS_, bytes tx_) onlyActive { // ~ 3.5k gas
    validateSignatures(sigV_, sigR_, sigS_, tx_); // ~ 11k gas

    var (nn, gp, gl, to, am, dt) = decodeTransaction(tx_); // ~ 3.5k gas

    require(nn == fullNonce()); // check nonce
    require(gp == tx.gasprice); // check gas price

    nonce = nonce + 1;

    require(to.call.gas(gl).value(am)(dt));
  }
}
